buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://jetbrains.jfrog.io/jetbrains/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }

    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.4"
    }
}

apply plugin: 'base'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.jfrog.artifactory"

apply from: "$rootDir/gradle/common/dependencies.gradle"

description = 'Spek Distribution packages'

evaluationDependsOn ':spek-api'
evaluationDependsOn ':spek-junit-platform-engine'

publishing {
    publications {
        api(MavenPublication) {
            groupId = 'org.jetbrains.spek'
            artifactId = 'spek-api'
            version = rootProject.version

            from project(':spek-api').components.java
            artifact project(':spek-api').tasks['sourceJar']
            artifact project(':spek-api').tasks['javadocJar']

            pom.withXml {
                asNode().dependencies.dependency.each {
                    it.scope[0].value = ('compile')
                }

                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name 'Spek'
                    description 'A Specification Framework for the JVM'
                    url 'https://jetbrains.github.io/spek'
                    licenses {
                        license {
                            name 'BSD New'
                            url 'https://github.com/JetBrains/spek/blob/master/LICENSE.TXT'
                            distribution 'spek'
                        }
                    }
                    developers {
                        developer {
                            id 'JetBrains'
                            name 'Spek Contributors'
                            organization 'JetBrains'
                        }
                    }
                    scm {
                        connection 'scm:git:git://github.com/jetbrains/spek.git'
                        developerConnection 'scm:git:https://github.com/jetbrains/spek.git'
                        url 'http://github.com/jetbrains/spek'
                    }
                }
            }
        }

        engine(MavenPublication) {
            groupId = 'org.jetbrains.spek'
            artifactId = 'spek-junit-platform-engine'
            version = rootProject.version

            from project(':spek-junit-platform-engine').components.java
            artifact project(':spek-junit-platform-engine').tasks['sourceJar']
            artifact project(':spek-junit-platform-engine').tasks['javadocJar']

            pom.withXml {
                asNode().dependencies.dependency.each {
                    it.scope[0].value = ('compile')
                }

                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name 'Spek JUnit Platform Engine'
                    description 'Spek TestEngine for the JUnit Platform'
                    url 'https://jetbrains.github.io/spek'
                    licenses {
                        license {
                            name 'BSD New'
                            url 'https://github.com/JetBrains/spek/blob/master/LICENSE.TXT'
                            distribution 'spek'
                        }
                    }
                    developers {
                        developer {
                            id 'JetBrains'
                            name 'Spek Contributors'
                            organization 'JetBrains'
                        }
                    }
                    scm {
                        connection 'scm:git:git://github.com/jetbrains/spek.git'
                        developerConnection 'scm:git:https://github.com/jetbrains/spek.git'
                        url 'http://github.com/jetbrains/spek'
                    }
                }
            }
        }
    }
}

bintray {
    user = "${bintrayUser}"
    key = "${bintrayApiKey}"
    publish = false

    pkg {
        repo = 'spek'
        desc = "Specification framework for the JVM"
        name = 'spek'
        userOrg = 'jetbrains'
        licenses = ['BSD New']
        labels = ['kotlin', 'testing', 'specification-framework']
        vcsUrl = 'https://github.com/JetBrains/spek.git'

        githubRepo = 'JetBrains/spek'


        version {
            name = rootProject.version
        }
    }

    publications = ['api', 'engine']
}

artifactory {
    if ([artifactory_contextUrl, artifactory_user, artifactory_password].contains("nothing")) {
        return
    }
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'spek-snapshots'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('api', 'engine')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
