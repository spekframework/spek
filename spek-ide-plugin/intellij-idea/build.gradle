apply from: "$rootDir/gradle/common/dependencies.gradle"
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin-platform-jvm'

def buildMatrix = [
        IJ182: [sdk: '2018.2.5', prefix: 'IJ2018.2', since: '182.1', until: '182.*', deps: ['org.jetbrains.kotlin:1.3.0-release-IJ2018.2-1']],
        IJ183: [sdk: '2018.3', prefix: 'IJ2018.3', since: '183.1', until: '183.*', deps: ['org.jetbrains.kotlin:1.3.0-release-IJ2018.3-1']]
]

String sdkVersion = "IJ183"

if (hasProperty('ij.version')) {
    sdkVersion = getProperty('ij.version')
}

def settings = buildMatrix[sdkVersion]

intellij {
    pluginName = 'Spek Framework'
    plugins = settings['deps']
    version = settings['sdk']
}

sourceSets {
    main {
        kotlin {
            srcDirs += "src/$sdkVersion/kotlin"
        }
    }
}

dependencies {
    compile project(':spek-ide-plugin:intellij-base-jvm')
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

buildPlugin {
    baseName = ''
    version = "${project.version}-${settings['prefix']}"
}

patchPluginXml {
    version = "${project.version}-${settings['prefix']}"
    sinceBuild = settings['since']
    untilBuild = settings['until']
}
